////////////////////////////////////////////////////////////////////
// A continuous state-action MDP for the classical cart-pole 
// system by Rich Sutton.
// 
// The goal here is to push a cart on a flat and frictionless surface 
// left or right by applying a force to the side of the cart, to 
// balance a pole on top of the cart. This domain is chosen to be
// difficult for short-sighted planners due to the relatively sparse
// reward structure, which assigns a reward of 1 while the pole
// has not fallen over. The actions in this domain are continuous in
// the range [-10, 10].
//
// References: 
//
// 		Barto, Andrew G., Richard S. Sutton, and Charles W. Anderson. 
// 		"Neuronlike adaptive elements that can solve difficult learning
// 		control problems." IEEE transactions on systems, man, and 
// 		cybernetics 5 (1983): 834-846.
//
// Author(s): 
//		Mike Gimelfarb (mgimelfarb@yahoo.ca)
//
////////////////////////////////////////////////////////////////////
domain cart_pole_continuous {

    requirements = {
        reward-deterministic
    };

    pvariables {

    	// forces
		GRAVITY   : { non-fluent, real, default = 9.8 };        // force of gravity acting down
		FORCE-MAX : { non-fluent, real, default = 10.0 };		// max force applied to the side of the cart

		// cart and pole properties
		CART-MASS : { non-fluent, real, default = 1.0 };        // mass of the cart
		POLE-MASS : { non-fluent, real, default = 0.1 };		// mass of the pole
		POLE-LEN  : { non-fluent, real, default = 0.5 }; 		// half of the pole length
		
		// other constants
		TIME-STEP : { non-fluent, real, default = 0.02 }; 		// seconds between state updates
		POS-LIMIT : { non-fluent, real, default = 2.4 };		// limit of cart position
		ANG-LIMIT : { non-fluent, real, default = 0.2094395 };	// limit of pole angle
	
		// interm & derived fluents
		temp : { interm-fluent, real };						// temporary variable
		acc : { interm-fluent, real };							// position acceleration
		ang-acc : { interm-fluent, real };						// angular acceleration

		// states
       	pos    	: { state-fluent, real, default = 0 };          // cart position
       	ang-pos	: { state-fluent, real, default = 0 };          // pole angle
       	vel   	: { state-fluent, real, default = 0 };          // cart velocity
       	ang-vel : { state-fluent, real, default = 0 };          // pole angular velocity

       	// actions
       	force : { action-fluent, real, default = 0 };      	// how much force to apply to the side of the cart
    };

    cpfs {
		
		// compute the pole angular acceleration
		temp = (force + POLE-LEN * POLE-MASS * pow[ang-vel, 2] * sin[ang-pos]) / (CART-MASS + POLE-MASS);
		ang-acc = (GRAVITY * sin[ang-pos] - cos[ang-pos] * temp) / (
            POLE-LEN * ((4.0 / 3.0) - (POLE-MASS * pow[cos[ang-pos], 2] / (CART-MASS + POLE-MASS))));
		
		// compute the cart acceleration
		acc = temp - (POLE-LEN * POLE-MASS * ang-acc * cos[ang-pos] / (CART-MASS + POLE-MASS));

		// Euler integration formula
		pos' = pos + TIME-STEP * vel;
		ang-pos' = ang-pos + TIME-STEP * ang-vel;
		vel' = vel + TIME-STEP * acc;
		ang-vel' = ang-vel + TIME-STEP * ang-acc;
    };

    // R = 1 as long as the pole is upright and the cart is not out of bounds
    reward = if (pos < -POS-LIMIT | pos > POS-LIMIT | ang-pos < -ANG-LIMIT | ang-pos > ANG-LIMIT) 
				then 0 else 1;

    termination {
		pos < -POS-LIMIT | pos > POS-LIMIT;
		ang-pos < -ANG-LIMIT | ang-pos > ANG-LIMIT;
    };

    state-invariants {
	
		// state is within limits
		pos >= -POS-LIMIT;
		pos <= POS-LIMIT;
		ang-pos >= -ANG-LIMIT;
		ang-pos <= ANG-LIMIT;
		
		// system is physically plausible
		GRAVITY >= 0 ^ FORCE-MAX >= 0;
		CART-MASS >= 0 ^ POLE-MASS >= 0 ^ POLE-LEN >= 0;
		TIME-STEP >= 0 ^ POS-LIMIT >= 0 ^ ANG-LIMIT >= 0;
    };

    action-preconditions {
        force >= -FORCE-MAX;
		force <= FORCE-MAX;
    };

}
